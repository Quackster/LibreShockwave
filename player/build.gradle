plugins {
    id 'java'
    id 'application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation project(':sdk')
    implementation project(':xtras')
}

application {
    mainClass = 'com.libreshockwave.player.swing.SwingPlayer'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.libreshockwave.player.swing.SwingPlayer'
    }
}

// Run network Xtra test
tasks.register('runNetXtraTest', JavaExec) {
    group = 'verification'
    description = 'Test NetLingo Xtra functions'
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.libreshockwave.player.NetXtraTest'
    dependsOn compileTestJava
}

// Fat JAR with all dependencies
tasks.register('fatJar', Jar) {
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'com.libreshockwave.player.swing.SwingPlayer'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}
